<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" height="100%" creationComplete="init();">
	<mx:Script>
		<![CDATA[
			
			import controllers.HomeController;
			import events.GerarExtratoEvent;
			import events.MostrarCompromissosEvent;
			import events.AdicionarCompromissoEvent;
			import events.GerarRelatorioEvent;
			import mx.controls.Alert;
			import util.AlertManager;
			import mx.collections.ArrayCollection;
			import events.AdicionarTransacaoEvent;
			import events.CadastrarContaEvent;
			import mx.events.MenuEvent;
			import util.AuthEntity;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			import events.DoLogoutEvent;
			
			private static const ADD_CONTA:String = "Adicionar Conta";
			private static const REMOVE_CONTA:String = "Remover Conta";			
			private static const ADD_TRANSACAO:String = "Adicionar Transação";
			private static const REMOVE_TRANSACAO:String = "Remover Transação";			
			private static const AGENDAR_TRANSACAO:String = "Agendar Transação";
			private static const RELATORIO:String = "Gerar Relatório";			
			private static const IMPORTAR:String = "Importar Extrato";
			private static const EXPORTAR:String = "Exportar Extrato";
			private static const MOSTRAR_COMPROMISSOS:String = "Mostrar Compromissos";
			
			private var accountsIds:ArrayCollection;
			
			private var timer:Timer = new Timer(300, 1);
			
			[Bindable]
			private var accountsLabels:ArrayCollection;
			
			[Bindable]
			private var ultimasOps:ArrayCollection;
			
			private function init():void {
				// utilizado para resolver bug do flex ao atualizar contas.
				timer.addEventListener(TimerEvent.TIMER, atualizarIndex);
				criarInterfacesExternas();
			}
			
			public function atualiza():void {
				carregaAccountsIds();
			}
			
			// utilizado pelo Selenium
			public function criarInterfacesExternas():void {
				ExternalInterface.addCallback("addConta", addConta);
				ExternalInterface.addCallback("addTransacao", addTransacao);
				ExternalInterface.addCallback("addCompromisso", agendarTransacao);
				ExternalInterface.addCallback("mostrarCompromissos", mostrarCompromissos);
				ExternalInterface.addCallback("getTransacoes", getTransacoes);
				ExternalInterface.addCallback("selecionaTransacao", selecionaTransacao);
				ExternalInterface.addCallback("removeTransacao", removeTransacao);
				ExternalInterface.addCallback("selecionaConta", selecionaConta);
				ExternalInterface.addCallback("removeConta", removeConta);
				ExternalInterface.addCallback("getNomeContas", getNomeContas);
				ExternalInterface.addCallback("gerarRelatorio", gerarRelatorio);
				ExternalInterface.addCallback("gerarEvento",exportar );
			
			}
			
			public function selecionaConta(index:Number):void {
				accountsList.selectedIndex = index;
			}
			
			public function selecionaTransacao(index:Number):void {
				transacoes.selectedIndex = index;
			}
			
			public function getNomeContas():String {
				return accountsLabels.toString();
			}
			
			public function getTransacoes():String {
				if (ultimasOps == null)
					return "";
				var retorno:ArrayCollection = new ArrayCollection();
				for (var i:int = 0; i < ultimasOps.length;i++) {
					var item:Object = ultimasOps.getItemAt(i); 
					retorno.addItem(item.amount + " - " + item.date + " - " + item.way + " - " + item.type);  
				}
				return retorno.toString();
			}
			
			private function carregaAccountsIds():void {
				var login:String = AuthEntity.getInstance().login;
				HomeController.getInstance().getAllAccountsIds(login, accountsIdsHandler, defaultFaultHandler);
				
			}
			
			private function accountsIdsHandler(e:ResultEvent):void {
				accountsIds = e.result as ArrayCollection;
				carregaAccountsLabels();
			}
			
			private function defaultFaultHandler(e:FaultEvent):void {
				AlertManager.showError(e);
			}
			
			private function carregaAccountsLabels():void {
				var login:String = AuthEntity.getInstance().login;
				HomeController.getInstance().getAccountsLabels(login, accountsIds, accountsLabelsHandler, defaultFaultHandler);
			}
			
			private function accountsLabelsHandler(e:ResultEvent):void {
				accountsLabels = e.result as ArrayCollection;
				atualizaAccId();
			}
			
			private function atualizaAccId():void {
				if (accountsIds.length > 0) {
					if (AuthEntity.getInstance().isAccIdReseted()) {
						accountsList.selectedIndex = 0;
						AuthEntity.getInstance().accId = accountsIds.getItemAt(accountsList.selectedIndex) as Number;
						atualizaUltimasOps();
					} else if (accountsList.selectedIndex < 0) {
						timer.start();
					}
				} else
					ultimasOps = new ArrayCollection(); 
			}
			
			private function atualizarIndex(e:TimerEvent):void {
				accountsList.selectedIndex = accountsIds.getItemIndex(AuthEntity.getInstance().accId);
				atualizaUltimasOps();
			}
			
			private function novaAccId():void {
				AuthEntity.getInstance().accId = accountsIds.getItemAt(accountsList.selectedIndex) as Number;
				atualizaUltimasOps();
			}
			
			private function atualizaUltimasOps():void {
				ultimasOps = new ArrayCollection();
				if (accountsIds.length > 0) {
					var accId:Number = AuthEntity.getInstance().accId;
					var login:String = AuthEntity.getInstance().login;
					HomeController.getInstance().getAllOperations(login, accId, getOperationsCallBack, defaultFaultHandler);
				}
			}
			
			private function getOperationsCallBack(e:ResultEvent):void {
				this.ultimasOps = e.result as ArrayCollection;
			}
			
			private function logout():void {				
				HomeController.getInstance().doLogout(AuthEntity.getInstance().login, AuthEntity.getInstance().senha, callBackLogout, callBackFault);
			}
			private function limparTudo():void {
				accountsLabels = new ArrayCollection();
				ultimasOps = new ArrayCollection();
				accountsIds = new ArrayCollection();
				AuthEntity.getInstance().login = "";
				AuthEntity.getInstance().resetAccId();
			}
			
			private function callBackLogout(e:ResultEvent):void {
				limparTudo();
				dispatchEvent(new DoLogoutEvent(DoLogoutEvent.LOGOUT));
			}

			private function callBackFault(e:FaultEvent):void {
				AlertManager.showError(e);
			}
			
			private function itemClickHandler(e:MenuEvent):void {
				if (e.label == ADD_CONTA) {
					addConta();
				} else if (e.label == REMOVE_CONTA) {
					removeConta();
				} else if (e.label == ADD_TRANSACAO) {
					addTransacao();
				} else if (e.label == REMOVE_TRANSACAO) {
					removeTransacao();
				} else if (e.label == AGENDAR_TRANSACAO) {
					agendarTransacao();
				} else if (e.label == MOSTRAR_COMPROMISSOS) {
					mostrarCompromissos();
				} else if (e.label == RELATORIO) {
					gerarRelatorio();
				}  else if (e.label == IMPORTAR) {
					importar();
				} else if (e.label == EXPORTAR) {
					exportar();
				} else {
					Alert.show("not match, e.label = " + e.label);
				}
			}
			
			private function addConta():void {
				dispatchEvent(new CadastrarContaEvent(CadastrarContaEvent.CADASTRAR));
			}
			
			private function removeConta():void {
				if (accountsIds.length > 0) {
					var accountIndex:int = accountsList.selectedIndex;
					var login:String = AuthEntity.getInstance().login;
					var accToRemoveId:Number = accountsIds.getItemAt(accountIndex) as Number;
					AuthEntity.getInstance().resetAccId();
					HomeController.getInstance().removeAccount(login, accToRemoveId, removerContaHandler, defaultFaultHandler); 
				}
			}
			
			private function removerContaHandler(e:ResultEvent):void {
				carregaAccountsIds();
			}
			
			private function removerOperacaoHandler(e:ResultEvent):void {
				atualizaUltimasOps();
			}
			
			private function defulatFaultHandler(e:FaultEvent):void {
				AlertManager.showError(e);
			}
			
			private function addTransacao():void {
				dispatchEvent(new AdicionarTransacaoEvent(AdicionarTransacaoEvent.ADICIONAR));
			}
			
			private function mostrarCompromissos():void {
				dispatchEvent(new MostrarCompromissosEvent(MostrarCompromissosEvent.EXIBIR));
			}
			
			private function removeTransacao():void {
				if (ultimasOps.length > 0) {
					var opIndex:int = transacoes.selectedIndex;
					if (opIndex >= 0) {
						var login:String = AuthEntity.getInstance().login;
						var opToRemoveId:Number = ultimasOps.getItemAt(opIndex).id;
						HomeController.getInstance().removeOperation(login, opToRemoveId, removerOperacaoHandler, defaultFaultHandler);
					} 
				}
			}
			
			private function agendarTransacao():void {
				dispatchEvent(new AdicionarCompromissoEvent(AdicionarCompromissoEvent.ADICIONAR));
			}
			
			private function gerarRelatorio():void {
				dispatchEvent(new GerarRelatorioEvent(GerarRelatorioEvent.GERARRELATORIO));
			}
			
			private function importar():void {
			}
			
			private function exportar():void {
				dispatchEvent(new GerarExtratoEvent(GerarExtratoEvent.GERAREVENTO));
			}

		]]>
	</mx:Script>
	<mx:Array id="contas">
		<mx:String>Conta Padrão</mx:String>
		<mx:String>Banco do Brasil</mx:String>
	</mx:Array>
	<mx:XMLList id="menu">
        <menuitem label="Conta" data="Conta">
        	<menuitem label="{ADD_CONTA}" data="{ADD_CONTA}"/>
        	<menuitem label="{REMOVE_CONTA}" data="{REMOVE_CONTA}"/>        	
        </menuitem>               
        <menuitem label="Transação" data="Transação">
        	<menuitem label="{ADD_TRANSACAO}" data="{ADD_TRANSACAO}"/>
        	<menuitem label="{REMOVE_TRANSACAO}" data="{REMOVE_TRANSACAO}"/>
        </menuitem>                                   
        <menuitem label="Compromisso" data="Compromisso">
        	<menuitem label="{AGENDAR_TRANSACAO}" data="{AGENDAR_TRANSACAO}"/>
        	<menuitem label="{MOSTRAR_COMPROMISSOS}" data="{MOSTRAR_COMPROMISSOS}"/>
        </menuitem>                                   
        <menuitem label="Extratos" data="Extratos">
        	<menuitem label="{RELATORIO}" data="{RELATORIO}"/>
        	<menuitem label="{IMPORTAR}" data="{IMPORTAR}"/>
        	<menuitem label="{EXPORTAR}" data="{EXPORTAR}"/>
        </menuitem>        
        
    </mx:XMLList>
	 <mx:HBox width="100%" height="100%">
	 	<mx:VBox height="100%">
		 	 <mx:Label text="MyMoney" fontSize="16" color="#CDEFC8" fontWeight="bold" letterSpacing="1" paddingTop="5"/>
			<mx:Panel title="Contas" height="100%">			
				<mx:List id="accountsList" dataProvider="{accountsLabels}" minWidth="160" width="100%" height="100%" selectedIndex="0" change="novaAccId();"/>		
			</mx:Panel>
	 	</mx:VBox>
	 	<mx:VBox width="100%" height="100%">
	 		<mx:HBox width="100%">
			 	 <mx:ApplicationControlBar id="dockedBar" dock="true" height="100%" width="100%">
			 	 	<mx:MenuBar height="100%" width="100%" dataProvider="{menu}" labelField="@label" itemClick="itemClickHandler(event);"/>
			 	 </mx:ApplicationControlBar>
			 	 <mx:Spacer width="30"/>
			 	 <mx:LinkButton label="Logout" id="logoutButtonHome" click="logout();"/>			
	 		</mx:HBox>
			<mx:Panel title="Movimentação Financeira" width="100%" height="100%">
				<mx:VBox width="100%" height="100%">
					<mx:DataGrid dataProvider="{ultimasOps}" id="transacoes" width="100%" height="100%">
				      <mx:columns>
				         <mx:DataGridColumn headerText="Valor" dataField="amount"/>
				         <mx:DataGridColumn headerText="Data" dataField="date"/>
				         <mx:DataGridColumn headerText="Forma de Pagamento" dataField="way"/>
				         <mx:DataGridColumn headerText="Tipo" dataField="type"/>
				      </mx:columns>
					</mx:DataGrid>		
				</mx:VBox>
			</mx:Panel>		
	 	</mx:VBox>	 	 
	 </mx:HBox>
	<mx:HBox>
	</mx:HBox>
</mx:VBox>
